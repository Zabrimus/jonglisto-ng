plugins {
    id 'java'
    id 'war'
    id 'eclipse-wtp'
    id "org.xtext.xtend" version '1.0.19'
}

apply plugin: 'war'

group 'jonglisto-ng'
version '0.0.1'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

apply from: "gradle/dependencies.gradle"

configurations.all {
   exclude group:"com.vaadin", module: "vaadin-client"
   exclude group:"com.vaadin", module: "vaadin-client-compiler"
}

dependencies {
    compileOnly "javax.servlet:javax.servlet-api:3.1.0"

    compile libs.guava
    compile libs.xtend

    compile project(':annotation')
    compile project(':common')
    compile project(':web')
    compile project(':svdrp')
    compile project(':database')

    // runtime 'com.vaadin.external.atmosphere:atmosphere-runtime:2.4.11.vaadin2'

    compile('com.vaadin:vaadin-cdi:3.0.0.alpha2') {
       transitive = false
    }

    compile 'org.apache.deltaspike.core:deltaspike-core-impl:1.8.0'

    compileOnly 'javax.enterprise:cdi-api:1.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.xtext.xtend'
    apply plugin: 'eclipse-wtp'

    repositories {
        jcenter()
        mavenCentral()

        maven {
            url "http://maven.vaadin.com/vaadin-addons"
        }

        maven {
            url "https://maven.vaadin.com/vaadin-prereleases"
        }
    }

    sourceSets {
        main.xtendOutputDir = 'src/main/xtend-gen'
        main.java.srcDirs += ['src/main/xtend-gen', 'src/main/resources', 'src/main/java']
    }

    dependencies {
        compile 'org.apache.commons:commons-lang3:3.5'
        compile 'commons-beanutils:commons-beanutils:1.9.3'
        compile 'commons-collections:commons-collections:3.2.2'
        compile 'org.apache.tomcat:tomcat-jdbc:8.5.20'
    }

    /*
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    */
}

project(':annotation') {
    dependencies {
        compile libs.xtend
        compile libs.guava
    }
}

project(':common') {
    dependencies {
        compile libs.xtend
        compile libs.gson
        compile libs.guava

        compile project(':annotation')
    }
}

project(':svdrp') {
    dependencies {
        compile libs.xtend
        compile libs.guava

        compile project(':annotation')
        compile project(':common')
    }
}

project(':database') {
    dependencies {
        compile libs.xtend
        compile libs.guava
        compile libs.sql2o
        compile libs.mysqljdbc

        compile project(':annotation')
        compile project(':common')
    }
}

project(':web') {
    dependencies {
        compile project(':annotation')
        compile project(':common')
        compile project(':svdrp')
        compile project(':database')

        compile libs.vaadinServer
        compile libs.vaadinThemes
        compile libs.vaadinClient
        compile libs.shirocore
        compile libs.shiroweb
        compile libs.sanitizer
        compile libs.guava
        compile libs.xtend

        compile('org.apache.shiro:shiro-config-ogdl:1.4.0') {
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'commons-collections', module: 'commons-collections'
        }

        compile addons.vicons

        compile(addons.scaleimage) {
            transitive = false
        }

        compile(addons.messagebox) {
            transitive = false
        }

        compile('com.vaadin:vaadin-cdi:3.0.0.alpha2') {
            transitive = false
        }

        compile 'org.apache.deltaspike.core:deltaspike-core-impl:1.8.0'

        compileOnly 'javax.enterprise:cdi-api:1.2'
        compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
        compileOnly "com.vaadin:vaadin-client:$versions.vaadin"
        compileOnly "com.vaadin:vaadin-client-compiler:$versions.vaadin"
    }
}

task createLogoJar(type: Exec) {
  executable "sh"
  args "-c", "cd tools; ./build-logo-jar.sh"
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.2.1'
}
